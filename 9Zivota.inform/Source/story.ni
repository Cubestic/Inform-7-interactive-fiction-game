"9Zivota" by Andrej

Part - Game setup

Chapter - Extensions

Include Achievements by Juhana Leinonen.
Include Vorple Command Prompt Control by Juhana Leinonen.
Include Vorple Hyperlinks by Juhana Leinonen.
Include Vorple Modal Windows by Juhana Leinonen.
Include Vorple Multimedia by Juhana Leinonen.
Include Vorple Notifications by Juhana Leinonen.
Include Vorple Screen Effects by Juhana Leinonen.
Include Vorple Status Line by Juhana Leinonen.
Include Vorple Tooltips by Juhana Leinonen.

Release along with the "Vorple" interpreter.

Chapter - Images

Release along with the file "heart.png".
Release along with the file "backpack.png".
Release along with the file "titlescreen.png".
Release along with the file "cardinaldirections.png".
Release along with the file "map.png".
Release along with the file "achievement.png".
Release along with the file "desk.png".
Release along with the file "note1.png".
Release along with the file "note2.png".
Release along with the file "note3.png".
Release along with the file "note4.png".
Release along with the file "note5.png".
Release along with the file "note6.png".

Chapter - Audio

Release along with the file "v1.mp3".
Release along with the file "v2.mp3".
Release along with the file "v3.mp3".
Release along with the file "v4.mp3".
Release along with the file "v5.mp3".

Release along with the file "v6.mp3".
Release along with the file "v7.mp3".
Release along with the file "v8.mp3".
Release along with the file "v9.mp3".
Release along with the file "v10.mp3".
Release along with the file "v11.mp3".

Release along with the file "achievement.mp3".
Release along with the file "wilhelm.mp3".

Chapter - Font

Release along with the file "Tagesschrift-Regular.ttf".

Chapter - HTML

Release along with the file "map.html".

Chapter - CSS

Release along with style sheet "my.css".

Part - Rooms

The Garage is a room. "This is where you spend your nights resting. In the corner lie two beds without blankets or pillows. It looks like your roommate is already up. You look tired and grumpy, as if you hadn't slept at all last night. Despite your exhaustion, it is time to get going."

The Hallway is a room. It is north of the Garage. "A long, cold hallway stretches before you, disappearing into darkness. You are not afraid of the dark, and it doesn't obstruct your vision."

The Storeroom is a room. It is east of the Garage. "A cluttered storeroom filled with various junk. While these objects hold no value to you, the maze of clutter makes a perfect hiding spot from your annoying roommate." 

The Entrance hall is a room. "It’s freezing in here. The smell of old wood hangs in the air. The floor tiles are worn down, but at least they're clean. The walls are decorated with yellow wallpaper bearing a floral pattern you can barely make out anymore. A wooden coat rack stands empty, which means there are no guests. That cheers you up, especially since it means you don’t have to put on a friendly face. An old umbrella propped in its stand always gives you a fright.[line break]In the distance, you hear a familiar voice calling your name. You decide to follow it."

The Bathroom is a room. It is west of the Entrance hall. "The bathroom is tiled in blue from floor to ceiling. By the gods, what an enormous bathtub. Who enjoys taking baths anyway? You get yourself clean and presentable just fine with your tongue. 'Looking good', you always think when you catch your reflection in the large bathroom mirror. Something about this room puts you on edge. Maybe it’s the water dripping from the sink faucet - someone didn’t turn it off properly. Water’s never really been your thing."

The Utility room is a room. It is west of Bathroom. "The room is packed with big, heavy appliances. The noise in here is unbearable. You remember seeing your mum bring your blankets here and return them clean, but with that awful smell. It always annoyed you because you couldn’t sleep on them until the scent wore off. Also, the last time you were here, the ironing board came crashing down, missing your head by an inch. Teo said you might be able to find a way to restore the power somewhere in this room."

The Living room is a room. "Finally, a place where you can lie down for a bit and relax. The living room couch looks comfortable despite the sagging cushions. Strange, unfamiliar voices keep coming from the box on the cabinet. You should probably turn it off. The carpet feels soft under your feet, nice enough that you could just stretch out on the floor. It’s odd that no one is around since you’re not supposed to be in here alone. Despite the overwhelming coziness, you sense a looming danger coming from your right.[line break]You hear a well-known sound coming from the room ahead. Better hurry."

The Library is a room. "Massive wooden shelves line the walls, stretching almost to the ceiling. They are full of books. A beautiful, slightly dusty chandelier hangs from the ornately carved wooden ceiling. It’s eerily quiet, maybe you could catch a quick nap here. In the center sits a writing desk with a high-backed armchair that doesn’t look particularly comfortable. Floor marks in front of one of the bookshelves catch your eye."

The Basement is a room. "Cobwebs hang everywhere, and the space is crammed with boxes. Old furniture and shelves overflow with dusty jars and bottles. Your curiosity gets the better of you - you just have to see what’s inside that slightly open box. A web wraps around your head, and no matter how hard you try, you can't shake it off. The dust makes you sneeze uncontrollably. As you fumble your way forward, you bump into a large object.[line break]The woman's voice can't be heard in here."

The Gallery is a room. It is north of the Bathroom. "The walls are filled with various imagery. Some of it you recognize, while other pieces you don’t understand and can barely describe. You spot your dull roommate in a few of them, but there’s no sign of yourself. In the corner stands a small table with a porcelain vase that looks ready to topple every time you pass by. To make matters worse, your neighbour is here. Who let him in? You hate that guy, his pushiness and his constant howling scare you. This isn’t a place to linger."

SobaA is a room. It is west of Gallery. The printed name of SobaA is "Balcony". "The balcony connects the Gallery with the Closet but isn’t used very much. It’s old and in dire need of repair. There’s a bit of everything here: several flower pots with plants that look like they’re about to give up. Last time you 'watered' the plants, you were scolded. A large green sun umbrella casts shade across the entire balcony. Underneath it, the watering can stands empty. Shame, you could use a refreshing drink right now. The view is beautiful, except for those ragged crows screeching overhead. They’re too big to hunt. To move across the balcony, you jump onto the balcony railing."
SobaB is a room. It is north of SobaA. The printed name of SobaB is "Balcony".
SobaC is a room. It is north of SobaB. The printed name of SobaC is "Balcony".

The Kitchen is a room. It is north of the Living room. "Ovo je opis koji nedostaje."

The Dining room is a room. It is west of the Kitchen and north of the Gallery. "Ovo je opis koji nedostaje."

The Pantry is a room. It is east of the Kitchen. "Ovo je opis koji nedostaje. Napisi jos da je blades na sjeveru."

The Closet is a room. It is east of SobaC. "Ovo je opis koji nedostaje."

The Attic is a room. It is north of the Closet. "Ovo je opis koji nedostaje."

The Study is a room. "Ovo je opis koji nedostaje."

[The Garden is a room. It is north of the Study. "Ovo je opis koji nedostaje."]

The Bedroom is a room. "Ovo je opis koji nedostaje."

Part - Doors
 
The brown door is a door.
It is north of the Entrance hall and south of the Living room.
It is closed, openable, locked and scenery.

The spike pit is a door.
It is east of the Living room and west of the Library.
It is open, not openable and scenery.

The secret door is a door.
It is south of the Library and north of the Basement.
It is closed, locked, scenery and undescribed.

The rotating blades is a door.
It is north of the Pantry and south of the Study.
It is open, not openable and scenery.

The red door is a door.
It is north of the Dining room and south of the Closet.
It is closed, openable, locked and scenery.

Part - Command

After issuing the response text of a response (called R):
	if R is parser clarification internal rule response (D) or R is parser clarification internal rule response (E):
		prefill the command line with "[parser command so far] ".
		
When play begins:
	choose row with a final response rule of immediately undo rule in the Table of Final Question Options;
	blank out the final question wording entry.
	
Use undo prevention.

Part - Dying

The player has a number called lives. The lives of the player is 9.
Death is a truth state that varies. Death is false. 

To kill the player in (respawn room - a room):
	decrease the lives of the player by 1;
	execute JavaScript command "document.querySelector('#heart-container .heart')?.remove();";
	if death is false:
		now death is true;
		play sound effect file "wilhelm.mp3";
		play music file "achievement.mp3";
		award the "8 more left" achievement;
	if the lives of the player is 0:
		say "[line break]You are out of lives!";
		execute JavaScript command "setTimeout(() => location.reload(), 5000);";
		end the story saying "[bold type]GAME OVER";
	otherwise:
		say "[line break]";
		place an inline element called "death";
		display text "YOU DIED" in the element called "death";
		say "[line break]";
		now the player is in the respawn room;
		if Vorple is supported:
			execute JavaScript command "localStorage.setItem('currentRoom', '[printed name of respawn room]')".
		
Check going through a spike pit:
	say "I can't get over this, but I might be able to swing across using something.";
	if a rope is worn:
		say "I made it! This rope will come in handy on the way back too.";
		continue the action;
	otherwise if a rope is carried:
		say "A rope is more useful if you [italic type]wear [roman type]it, not just have it in your inventory.";
		play music file "wilhelm.mp3";
		kill the player in the Living room;
		stop the action;
	otherwise:
		play music file "wilhelm.mp3";
		kill the player in the Living room;
		stop the action.
		
Check going through a rotating blades:
	say "I can't get through this without protection.";
	if armor is worn:
		say "I made it!";
		continue the action;
	otherwise if armor is carried:
		say "Armor is more useful if you [italic type]wear, [roman type]it."; 
		play music file "wilhelm.mp3";
		kill the player in the Pantry;
		stop the action;
	otherwise:
		play music file "wilhelm.mp3";
		kill the player in the Pantry;
		stop the action.
		
Check opening the black chest:
	if the black chest is locked:
		say "I need a key to open this.";
	otherwise:	
		say "This is better left unopened.";
		play music file "wilhelm.mp3";
		kill the player in the Closet;
		stop the action.
		
Check going to the Gallery [for the first time]:
	say "You hear loud growling coming from the gallery.";
	if the perfume is carried:
		say "This better work.";
	otherwise:
		say "Two bright eyes are the last thing you see as you enter the Gallery.";
		play music file "wilhelm.mp3";
		kill the player in the Bathroom;
		stop the action.
		
Check going north from SobaA:
	if the weight of the player > 2:
		say "You are too heavy!";
		play music file "wilhelm.mp3";
		kill the player in the Gallery;
		stop the action.
		
Check going north from SobaB:
	if the weight of the player > 1:
		say "[line break]You are still too heavy.";
		play music file "wilhelm.mp3";
		kill the player in the Gallery;
		stop the action.

Part - Inventory
		
The carrying capacity of the player is 2.

Carry out taking inventory (this is the print inventory using HTML lists rule):
	if Vorple is supported:
		say "[We] [are] carrying:[line break]";
		open HTML tag "ul";
		repeat with item running through things carried by the player:
			place "li" element reading "[item]";
			if the item contains something:
				open HTML tag "ul";
				repeat with content running through things contained by the item:
					place "li" element reading "[content]";
				close HTML tag;
		close HTML tag;
	otherwise:
		follow the print standard inventory rule.
		
The print inventory using HTML lists rule is listed instead of the print standard inventory rule in the carry out taking inventory rules.

Chapter - Backpack

The backpack is a player's holdall. The backpack has carrying capacity 3.

To update capacity counter:
	let items be the number of things carried by the player;
	let items_in_backpack be 0;
	if the player carries the backpack:
		repeat with item running through things in the backpack:
			increase items_in_backpack by 1;
	let total_items be items + items_in_backpack;
	let displayed_capacity be the carrying capacity of the player;
	if the player carries the backpack:
		let backpack_capacity be the carrying capacity of the backpack;
		increase displayed_capacity by backpack_capacity;
	let indicator_text be "[total_items]/[displayed_capacity]";
	execute JavaScript command "
		const capacityDiv = document.getElementById('capacity-indicator');
		capacityDiv.textContent = '[indicator_text]';
		if([total_items] >= [displayed_capacity]) {
			capacityDiv.classList.add('capacity-full');
			capacityDiv.classList.remove('capacity-available');
		} else {
			capacityDiv.classList.add('capacity-available');
			capacityDiv.classList.remove('capacity-full');
		}
	".
	
To display capacity counter:
	execute JavaScript command "
		const capacityDiv = document.createElement('div');
		capacityDiv.id = 'capacity-indicator'; capacityDiv.textContent = '0/2';
		capacityDiv.classList.add('capacity-indicator');
		document.body.appendChild(capacityDiv);
	";
	update capacity counter.
	
After taking something:
	update capacity counter;
	continue the action.

After dropping something:
	update capacity counter;
	continue the action.

After wearing something:
	update capacity counter;
	continue the action.
	
After taking off something:
	update capacity counter;
	continue the action.

Instead of taking the backpack:
	if the player carries a backpack:
		say "You already have a backpack.";
	otherwise:
		say "You pick up the backpack.";
		now the player carries the backpack;
		increase the weight of the player by 1;
		increase the carrying capacity of the player by 1;
		update capacity counter.

After dropping the backpack:
	decrease the weight of the player by 1;
	decrease the carrying capacity of the player by 1;
	say "You drop the backpack, reducing how much you can carry.";
	update capacity counter;
	let backpack_items be the number of things in the backpack;
	if backpack_items is greater than 0:
		say "The contents of your backpack spill out onto the ground: ";
		repeat with item running through things in the backpack:
			say "[line break][item]";
			decrease the weight of the player by 1;
			now item is in the location;
		say "[line break]".
	
[Instead of giving something to someone when the noun is in the backpack:
	decrease the weight of the player by 1;
	now the second noun carries the noun;
	say "You give [the noun] to [the second noun].";
	update capacity counter.]
			
Part - Status line

When play begins:
	construct the Vorple status line with 3 columns.
	
The left hand Vorple status line is "Weight: [weight of the player]".
The middle Vorple status line is "Coins: [price of the player]".
The right hand Vorple status line is "[time of day]". The time of day is 9:50 AM.

Part - Map

To open map:
	execute JavaScript command "
		let map = document.createElement('div');
		map.id = 'map';
		map.onclick = () => vorple.prompt.submit('map');
		document.body.appendChild(map);
	". 

Map is an action applying to nothing. Understand "map" as map.

Carry out map:
	if Vorple is supported:
		place a link to web site "map.html" reading "Open map [line break]";
		
After going:
	if Vorple is supported:
		execute JavaScript command "localStorage.setItem('currentRoom', '[printed name of location]')";
	continue the action.
		
Mapping is a truth state that varies. Mapping is false.
		
After going to the Bathroom:
	if mapping is false:
		now mapping is true;
		open map;
	continue the action.

Part - Title screen

When play begins:
	if Vorple is supported:
		place an image "titlescreen.png" with the description "Titlescreen", centered;
	center "[fantasy font style][play the game][end style]";
	wait for the SPACE key;
	clear the screen.
	
To say play the game:
	if Vorple is supported:
		place a link to execute the JavaScript command "vorple.prompt.queueKeypress(' ')" reading "PLAY".
		
Rule for printing the banner text: do nothing.
			
Part - Tutorial

Chapter - Player's name

The player's full name is a text that varies.

When play begins:
	now the command prompt is "What is your name? > ".

To decide whether collecting names:
	if the command prompt is "What is your name? > ", yes;
	no.

After reading a command when collecting names:
	let N be the number of characters in the player's command;
	if N < 2:
		say "[line break]Too short. Try again!";
		reject the player's command;
	if N > 10:
		say "[line break]Whoa! Let's keep it under 10 letters, superstar. Try a shorter name.";
		reject the player's command;
	now the player's full name is the player's command;
	now the command prompt is ">";
	say "[line break]Hi, [bold type][player's full name][roman type]! I’m glad you’re back. You've been gone for quite some time. Let’s remember how things work around here.[line break]";
	play music file "v1.mp3 ";
	move the player to the Garage, without printing a room description;
	display tooltip "Try to LOOK around you." on the prompt in 9 seconds;
	reject the player's command.
	
Instead of looking when collecting names: do nothing.
Rule for printing the banner text when collecting names: do nothing.
Rule for constructing the status line when collecting names: do nothing.

Chapter - Garage

The brush is in the Garage.
It is undescribed.

The default tooltip duration is 8.

Tooltip1 is a truth state that varies. Tooltip1 is true.

After looking:
	if the location is the Garage and Tooltip1 is true:
		hide the tooltip;
		play music file "v2.mp3 ";
		display tooltip "Move forward by typing NORTH or by pressing the red button." on the prompt in 18 seconds;
		directions;
		move north;
		now Tooltip1 is false;
	else if the location is the Garage and the player does not carry the first coin:
		display tooltip "You can go right by typing EAST or by pressing the yellow button." on the prompt;
		move east.
		
Rule for printing the name of the brush:
	if the location is the Garage:
		place an element called "brush-text" reading "brush";
		display a tooltip "This wasn't here before. I'd better take it." on the element called "brush-text" [in 1 seconds];
	otherwise:
		say "brush";
		
Chapter - Hallway
			
After going to the Hallway for the first time:
	hide the tooltip;
	try looking;
	play music file "v3.mp3 ";
	display tooltip "To get back type SOUTH or press the blue button." on the prompt in 10 seconds;
	move south.
	
Chapter - Storeroom

The coffer is a container.
It is in the Storeroom.
It is closed, openable and fixed in place.
	
After going to the Storeroom for the first time:
	hide the tooltip;
	try looking;
	play music file "v4.mp3";
	display tooltip "Try inspecting items by typing EXAMINE [bracket]item name[close bracket]." on the prompt in 11 seconds .
	
Instead of examining the coffer when the coffer is closed:
	say "A small ornate chest that you've never seen open. The coffer is ";
	place an element called "chest-text" reading "closed";
	display a tooltip "You can OPEN the coffer to see what's inside." on the element called "chest-text" in 1 seconds;
	say ".".
	
A coin is a kind of thing.
The first coin is a coin in the coffer.

Rule for printing the name of the first coin when the first coin is in the coffer:
	place an element called "coin-text" reading "coin";
	display a tooltip "The coin is something you can TAKE." on the element called "coin-text" in 1 seconds.
	
After taking the first coin:
	say "Taken.[line break]";
	increase the weight of the player by 1;
	if we have not taken the first coin:
		display tooltip "Try typing INVENTORY or click the backpack icon." on the prompt;
		check inventory;
		display capacity counter;
	otherwise:
		update capacity counter.
	
After taking inventory:
	if the player is in the Storeroom and the player carries the first coin:
		display tooltip "Time to get back! Press the green button or type WEST." on the prompt [in 3 seconds];
		move west;
		now the brush is described.

To directions:
	execute JavaScript command "
		let cd = document.createElement('div');
		cd.id = 'directions';
		document.body.appendChild(cd);
	".

To move north:
	execute JavaScript command "
		let n = document.createElement('div');
		n.id = 'north';
		n.textContent = 'N';
		n.onclick = () => vorple.prompt.submit('north');
		document.body.appendChild(n);
	".

To move east:
	execute JavaScript command "
		let e = document.createElement('div');
		e.id = 'east';
		e.textContent = 'E';
		e.onclick = () => vorple.prompt.submit('east');
		document.body.appendChild(e);
	".

To move south:
	execute JavaScript command "
		let s = document.createElement('div');
		s.id = 'south';
		s.textContent = 'S';
		s.onclick = () => vorple.prompt.submit('south');
		document.body.appendChild(s);
	".

To move west:
	execute JavaScript command "
		let w = document.createElement('div');
		w.id = 'west';
		w.textContent = 'W';
		w.onclick = () => vorple.prompt.submit('west');
		document.body.appendChild(w);
	".

To check inventory:
	execute JavaScript command "
		let bp = document.createElement('div');
		bp.id = 'backpack';
		bp.onclick = () => vorple.prompt.submit('inventory');
		document.body.appendChild(bp);
	".

Chapter - Difficulty 
		
Difficulty is a kind of value. The difficulties are normal, hard, unforgiving and unknown.
Understand "normal" as normal.
Understand "hard" as hard.
Understand "unforgiving" as unforgiving.

The player has a difficulty. The difficulty of the player is unknown.

Tezina is a truth state that varies. Tezina is false.
Biranje is a truth state that varies. Biranje is false.

Instead of taking the brush when the first coin is carried and biranje is false:
	move the brush to the player;
	increase the weight of the player by 1;
	update capacity counter;
	hide the tooltip;
	say "You pick up the brush.[paragraph break]Oh, one more thing before you leave.";
	play music file "v5.mp3 ";
	now tezina is true;
	now the command prompt is "Please select difficulty >";
	display tooltip "Type UNKNOWN for help." on the prompt in 3 seconds;
	
Instead of taking the brush when the first coin is carried and biranje is true:
	now the player carries the brush;
	increase the weight of the player by 1;
	say "You pick up the brush.";
	
After reading a command when tezina is true:
	if the player's command includes "[difficulty]":
		now the difficulty of the player is the difficulty understood;
		if the difficulty of the player is unknown:
			say "Please select: 'normal' 'hard' or 'unforgiving'. [run paragraph on]"; [ovo popravi]
			reject the player's command;
		if the difficulty of the player is normal:
			now the lives of the player is 9;
			execute JavaScript command "
				const container = document.querySelector('#heart-container');
			  container.innerHTML = '';
			  for (let i = 0; i < 9; i++) {
				const heart = document.createElement('div');
				heart.className = 'heart';
				container.appendChild(heart);
			  }
			";
		if the difficulty of the player is hard:
			now the lives of the player is 3;
			execute JavaScript command "
			  const container = document.querySelector('#heart-container');
			  container.innerHTML = '';
			  for (let i = 0; i < 3; i++) {
				const heart = document.createElement('div');
				heart.className = 'heart';
				container.appendChild(heart);
			  }
			";
		if the difficulty of the player is unforgiving:
			now the lives of the player is 1;
			execute JavaScript command "
			  const container = document.querySelector('#heart-container');
			  container.innerHTML = '';
			  for (let i = 0; i < 1; i++) {
				const heart = document.createElement('div');
				heart.className = 'heart';
				container.appendChild(heart);
			  }
			";
		say "[line break]Your journey continues...";
		now the command prompt is ">";
		now tezina is false;
		now biranje is true;
		hide the tooltip;
		move the player to the Entrance hall;
		check achievements;
		play sound effect "achievement.mp3";
		award the "Getting started" achievement;
		play music file "v6.mp3";
		reject the player's command;
	otherwise:
		say "Please select a difficulty to continue. [run paragraph on]";
		reject the player's command.
		
Part - Entrance hall

The entrance hall note is on the desk in the Entrance hall. The printed name of the entrance hall note is "note".
The description is "[player's full name] isn't allowed to go in. The door is locked, and the key's in the drawer. I wrote down how to open it, just in case you forgot.[line break]D."

After examining the entrance hall note:
	if Vorple is supported:
		place an image "note1.png" with the description "Note1", centered.
		
Flip is an action applying to one visible thing.
Understand "flip [something]" as flip.
Understand "turn [something] over" as flip.
Understand "turn over [something]" as flip.

Check flip:
	if the noun is the entrance hall note:
		place an image "note2.png" with the description "Note2", centered.

The desk is in the Entrance hall.
The description is "A small table with four painted drawers, one of them faded.".
Rule for writing a paragraph about the desk:
	if the entrance hall note is on the desk:
		say "You can see a desk here.";
	otherwise:
		say "You can see a desk here.".

After examining the desk:
	if Vorple is supported:
		place an image "desk.png" with the description "Desk", centered.
				
The pink drawer, the green drawer, the red drawer and the blue drawer are parts of the desk.
The pink drawer, the green drawer, the red drawer and the blue drawer are openable closed containers.
The printed name of the pink drawer is "faded drawer".

The secret drawer is a part of the desk. The secret drawer is an openable closed container.
The secret drawer is scenery.

After opening the secret drawer:
	say "You open the secret drawer, revealing a brown key.[line break]";
	if we have not opened the secret drawer:
		play music file "achievement.mp3";
		award the "StationPlay" achievement.

Instead of doing anything other than examining to the secret drawer when the secret drawer is scenery:
	say "Can't do that."
	
The brown key is in the secret drawer.
The brown key unlocks the brown door.

The second coin is a coin in the red drawer.
After taking the second coin:
	say "Taken.";
	increase the weight of the player by 1;
	update capacity counter.

The fuse is in the blue drawer.

The library note is in the pink drawer.
The description is "D,[line break]I have redesigned your bookcase as instructed. Your four favorite books have been placed at the specified spots and must be taken alphabetically."

After examining the library note:
	if Vorple is supported:
		place an image "note3.png" with the description "Note3", centered.
	
The desk has a truth state called sequence. The sequence of the desk is false.
The desk has a number called position. The position of the desk is 0.

Check opening the red drawer:
	if the sequence of the desk is false:
		now the sequence of the desk is true;
		now the position of the desk is 1;
	else if the position is not 0 :
		now the sequence of the desk is false;
		now the position of the desk is 0;
		
Check opening the blue drawer:
	if the position of the desk is 1:
		now the position of the desk is 2;
	else:
		now the sequence of the desk is false;
		now the position of the desk is 0;
		
Check opening the pink drawer:
	if the position of the desk is 2:
		now the position of the desk is 3;
	else:
		now the sequence of the desk is false;
		now the position of the desk is 0;
		
[Check opening the green drawer:
	if the green drawer is open:
		say "That's already open." instead;
	if the position of the desk is 3:
		now the position of the desk is 4;
		now the secret drawer is not scenery;
		say "As you open the green drawer, you hear a click and a secret drawer appears in the desk!";
	else:
		now the sequence of the desk is false;
		now the position of the desk is 0;
		say "You open the green drawer." instead.]
		
Before opening the green drawer:
	if the position of the desk is 3:
		now the position of the desk is 4;
		now the secret drawer is not scenery;
		say "As you open the green drawer, you hear a click and a secret drawer appears in the desk!";
		now the green drawer is open;
	else:
		now the sequence of the desk is false;
		now the position of the desk is 0.
	
Part - Bathroom

After going to the Bathroom for the first time:
	play music file "v7.mp3";
	continue the action.

Teodor is a man in the Bathroom. The description of Teodor is "Ovo je opis Teodora.".

Table of Teodor Responses
Topic	Response			Index	Asked
"Topic1"	"Razgovor1"			"Topic1"	false
"Topic2"	"Razgovor2"			"Topic2"	false
"Topic3"	"Razgovor3"			"Topic3"	false
	
To decide whether all topics are discussed:
	repeat through the Table of Teodor Responses:
		if the Asked entry is false:
			no;
	yes.

backpack_taken is a truth state that varies. backpack_taken is false.

After asking Teodor about something:
	if the topic understood is a topic listed in the Table of Teodor Responses:
		say "[Response entry][line break]";
		now the Asked entry is true;
		if all topics are discussed and backpack_taken is false:
			say "[line break]Teodor drops a backpack at your feet.";
			now the backpack is in the Bathroom;
			now the backpack_taken is true.

After examining Teodor:
	say "Topics you can talk about with Teodor:[line break]";
	repeat with N running from 1 to the number of rows in the Table of Teodor Responses:
		choose row N in the Table of Teodor Responses;
		say " - ";
		place a link to command "ask Teodor about [Index entry]" reading "[Index entry]";
		if the Asked entry is true:
			say " (discussed)";
		say "[line break]".

Rule for printing the name of Teodor:
	place a link to command "examine [the printed name of the item described]" reading "[the printed name of the item described]";

Part - Utility room

After going to the Utility room for the first time:
	play music file "v8.mp3";
	continue the action.

The generator is a switched off device in the Utility room. It is fixed in place.

The description of the generator is "Ovo je opis generatora. [paragraph break][generator-status]".

To say generator-status:
	say "Fuse: ";
	if the player carries the fuse:
		say "1/1";
	else:
		say "0/1";
	say " | Fuel: ";
	if the player carries the fuel can:
		say "1/1";
	else:
		say "0/1".
		
Instead of switching on the generator:
	if the generator is switched on:
		say "That's already on.";
	else if the player does not carry the fuel can and the player does not carry the fuse:
		say "You need a fuse and fuel.";
	else if the player does not carry the fuel can:
		say "You need fuel.";
	else if the player does not carry the fuse:
		say "You are missing a fuse.";
	else:
		say "You switch the generator on.";
		now the generator is switched on;
		play music file "achievement.mp3";
		award the "Let there be light" achievement;
		remove the fuel can from play;
		remove the fuse from play;
		decrease the weight of the player by 2;
		update capacity counter.
		
Part - Living room

After going to the Living room for the first time:
	play music file "v11.mp3";
	continue the action.
	
The golden chest is a container.
It is in the Living room.
It is closed, openable and locked.

The money is in the golden chest.
The description is "Gold coins of various shapes. Some might find them strange, but not you."

Part - Library

After going to the Library for the first time:
	play music file "v9.mp3";
	continue the action.

The fourth coin is a coin in the Library.
After taking the fourth coin:
	say "Taken.";
	increase the weight of the player by 1;
	update capacity counter.

The bookshelf is a supporter in the Library. The description is "[if we have taken the library note]This looks familiar". It is scenery.
After examining the bookshelf:
	if Vorple is supported:
		place an image "note5.png" with the description "Note5", centered.

A puzzle-book is a kind of thing.
The wheel book, the crown book, the sun book, and the star book are puzzle-books.

Instead of taking a puzzle-book:
	say "The book seems firmly attached to the shelf. You can only pull it."

The star book is a puzzle-book on the bookshelf. The star book is undescribed.
The crown book is a puzzle-book on the bookshelf. The crown book is undescribed.
The wheel book is a puzzle-book on the bookshelf. The wheel book is undescribed.
The sun book is a puzzle-book on the bookshelf. The sun book is undescribed.

The bookshelf has a truth state called sequence. The sequence of the bookshelf is false.
The bookshelf has a number called position. The position of the bookshelf is 0.

Instead of pulling the crown book:
	say "Authors: Blaženka Divjak, Tihomir Hunjak[line break]";
	if the sequence of the bookshelf is false:
		now the sequence of the bookshelf is true;
		now the position of the bookshelf is 1;
	else if the position of the bookshelf is not 0:
		now the sequence of the bookshelf is false;
		now the position of the bookshelf is 0.

Instead of pulling the star book:
	say "Authors: Miroslav Žugaj, Ruža Brčić[line break]";
	if the position of the bookshelf is 1:
		now the position of the bookshelf is 2;
	else:
		now the sequence of the bookshelf is false;
		now the position of the bookshelf is 0.

Instead of pulling the sun book:
	say "Authors: Alen Lovrenčić, Mario Konecki[line break]";
	if the position of the bookshelf is 2:
		now the position of the bookshelf is 3;
	else:
		now the sequence of the bookshelf is false;
		now the position of the bookshelf is 0.

Instead of pulling the wheel book:
	say "Authors: Mirko Maleković, Markus Schatten[line break]";
	if the position of the bookshelf is 3:
		now the position of the bookshelf is 4;
		now the secret door is unlocked;
		now the secret door is open;
		now the secret door is not undescribed;
		play music file "achievement.mp3";
		award the "Mate&Teo" achievement;
		say "[line break]You hear a soft click. The bookshelf shifts, revealing a hidden passage.";
	else:
		now the sequence of the bookshelf is false;
		now the position of the bookshelf is 0.
		
Part - Basement

After going to the Basement for the first time:
	play music file "v10.mp3";
	continue the action.

The game machine is a switched off device in the Basement.

Instead of switching on the game machine:
	if the generator is switched off:
		say "The power's out!";
	else:
		now the game machine is switched on;
		say "You switch the machine on.[paragraph break]Insert coin to play!".
	
A reward is a kind of thing. Some rewards are defined by the Table of Prizes.
	
Table of Prizes
Prize	Description
golden key	"Opis"

The golden key unlocks golden chest.
After taking the golden key:
	if we have not taken the golden key:
		play music file "achievement.mp3";
		award the "Best candidate for the job" achievement;
	say "Taken.[line break]";
	increase the weight of the player by 1;
	update capacity counter.

The game machine has a truth state called loaded. The loaded of the game machine is false.
The game machine has a number called times_won. The times_won of the game machine is 0.

Instead of inserting a coin into the game machine:
	if the game machine is switched off:
		say "You should turn on the game machine first.";
	else:
		remove the noun from play;
		decrease the weight of the player by 1;
		update capacity counter;
		now the loaded of the game machine is true;
		say "You insert the coin into the machine. The machine lights up![paragraph break]";
		say "Press ";
		place a link to command "start" reading "START";
		say " to play.";
		say "Read ";
		place a link to command "rules" reading "RULES";
		say ".";
		say "View ";
		place a link to command "prizes" reading "PRIZES";
		say ".".
		
The game machine has a number called current_question. The current_question of the game machine is 0.
The game machine has a number called correct_answers. The correct_answers of the game machine is 0.
The game machine has a truth state called quiz_active. The quiz_active of the game machine is false.

Release along with the file "trivia1.png".
Release along with the file "trivia2.png".
Release along with the file "trivia3.png".
Release along with the file "trivia4.png".
Release along with the file "trivia5.png".
Release along with the file "trivia6.png".
Release along with the file "trivia7.png".
Release along with the file "trivia8.png".
Release along with the file "trivia9.png".
Release along with the file "trivia10.png".
Release along with the file "trivia11.png".
Release along with the file "trivia12.png".
Release along with the file "trivia13.png".
Release along with the file "trivia14.png".
Release along with the file "trivia15.png".
Release along with the file "trivia16.png".
Release along with the file "trivia17.png".
Release along with the file "trivia18.png".

Table of Trivia
Trivia	Answer	Image
"Which piece fits perfectly into the image?"	"B"	"trivia1.png"
"Which piece fits perfectly into the image?"	"G"	"trivia2.png"
"Which piece fits perfectly into the image?"	"C"	"trivia3.png"
"Which piece fits perfectly into the image?"	"E"	"trivia4.png"
"Which piece fits perfectly into the image?"	"F"	"trivia5.png"
"Which piece fits perfectly into the image?"	"E"	"trivia14.png"
"Which piece fits perfectly into the image?"	"C"	"trivia15.png"
"Choose the matching figure."	"A"	"trivia6.png"
"Choose the matching figure."	"C"	"trivia7.png"
"Choose the matching figure."	"D"	"trivia8.png"
"Choose the matching figure."	"D"	"trivia9.png"
"Choose the matching figure."	"C"	"trivia10.png"
"Find the object that matches the one on the left."	"A"	"trivia11.png"
"Find the object that matches the one on the left."	"D"	"trivia12.png"
"Find the object that matches the one on the left."	"D"	"trivia13.png"
"Which one doesn't fit?"	"D"	"trivia16.png"
"Which one doesn't fit?"	"A"	"trivia17.png"
"Which one doesn't fit?"	"D"	"trivia18.png"
"How old is a person today if, in 38 years, they will be three times as old as they are now?" 	"19"	""
"When Tom was 6, Tim was half his age. If Tom is 40 today, how old is Tim?"	"37"	""
"An eagle flies at a height of 2 1/4 kilometers. During flight, it rises another 2500 meters. At what height is it now flying? (in meters)"	"4750"	""
"Marko wants to insure his car which is worth 100,000 euros. The insurance is paid at 3 percent of 4/5 of the car's value. How much does the insurance for that car cost?"	"2400"	""
"Ivan's office is 2 km away from his house. Walking on foot, he covers 5 km per hour. How much time does he spend over 5 days going to work and returning home?"	"4"	""
"One company works in 3 shifts. The company employs a total of 330 workers. In the first shift, 1/4 of the workers work, and in the second shift, 2/8 of the workers work. How many workers work in the third shift?"	"165"	""
"From a barrel of 56 liters, 3/4 of the wine has leaked out. How many deciliters of wine are left in the barrel?"	"140"	""
"A piece of paper 3 2/4 meters wide needs to be cut into strips 70 cm wide each. How many such strips can be cut?"	"5"	""
"Ivan bought two wooden beams, the first is 3.3 meters long, and the second is 2.465 meters long. How much longer is the first beam?"	"0.835"	""
"Marko paid 15 cents for one lollipop. How much will he pay for one dozen lollipops?"	"180"	""
"I have a head and a tail but nothing in between. What am I?"	"Coin"	""
"I have keys, but no locks, and no rooms. You can enter, but you can’t go outside. What am I?"	"Keyboard"	""
["What disappears as soon as you say its name?"	"Silence"	""]

Startgame is an action applying to nothing. Understand "start" as startgame.

Check startgame:
	if the player is not in the Basement:
		say "There's no game machine here." instead;
	if the game machine is switched off:
		say "The machine isn't turned on." instead;
	if the loaded of the game machine is false:
		say "You need to insert a coin first." instead.
		
Carry out startgame:
	now the quiz_active of the game machine is true;
	now the current_question of the game machine is 1;
	now the correct_answers of the game machine is 0;
	say "Welcome to the Trivia Challenge! Answer 5 questions correctly to win![paragraph break]";
	show next question.
	
Showrules is an action applying to nothing. Understand "rules" as showrules.

Carry out showrules:
	say "Pravila[line break]".

Showprizes is an action applying to nothing. Understand "prizes" as showprizes.

Carry out showprizes:
	say "You can win:[line break]";
	repeat through Table of Prizes:
		say " - [Prize entry][line break]".

To decide whether answering trivia:
	if the quiz_active of the game machine is true, yes;
	no.
	
The game machine has a list of numbers called asked_questions. The asked_questions of the game machine is {}.
The game machine has a number called last_asked_question. The last_asked_question of the game machine is 0.

To show next question:
	let available_questions be a list of numbers;
	repeat with N running from 1 to the number of rows in the Table of Trivia:
		if N is not listed in the asked_questions of the game machine:
			add N to available_questions;
	let max_index be the number of entries in available_questions;
	let random_index be a random number between 1 and max_index;
	let question_num be entry random_index of available_questions;
	add question_num to the asked_questions of the game machine;
	now the last_asked_question of the game machine is question_num;
	choose row question_num in the Table of Trivia;
	say "Question [current_question of the game machine]: [Trivia entry][paragraph break]";
	if Image entry is not "" and Vorple is supported:
		place an image Image entry with the description "Question image", centered;
		say "[paragraph break]";
	now the command prompt is "Your answer > ".

After reading a command when answering trivia:
	let player_answer be the player's command;
	choose row last_asked_question of the game machine in the Table of Trivia;
	if player_answer matches the text Answer entry:
		increase the correct_answers of the game machine by 1;
		say "[line break]Correct![paragraph break]";
		if the correct_answers of the game machine is 5:
			increase the times_won of the game machine by 1;
			say "Congratulations! You've answered all questions correctly! You win![paragraph break]";
			now the asked_questions of the game machine is {};
			if the times_won of the game machine is greater than the number of rows in the Table of Prizes:
				say "The machine is out of prizes!";
			else:
				choose row times_won of the game machine in the Table of Prizes;
				say "The machine makes a grinding noise and spits out [a prize entry]!";
				now the Prize entry is in the Basement;
			now the quiz_active of the game machine is false;
			now the loaded of the game machine is false;
			now the current_question of the game machine is 0;
			now the correct_answers of the game machine is 0;
			now the command prompt is ">";
		else:
			increase the current_question of the game machine by 1;
			show next question;
	else:
		say "[line break]Wrong answer! Better luck next time!";
		now the asked_questions of the game machine is {};
		now the quiz_active of the game machine is false;
		now the loaded of the game machine is false;
		now the current_question of the game machine is 0;
		now the correct_answers of the game machine is 0;
		now the command prompt is ">";
	reject the player's command.

Part - Gallery

The beast is a animal in the Gallery. The description is "Ovo je opis pasa. Reci da je gladan."
It carries the red key.

The red key is a thing.
It unlocks the red door.

The fuel can is in the Gallery.

Instead of giving the bone to the beast:
	update capacity counter;
	try the beast dropping the red key.
	
Part - Balcony

After going:
	if Vorple is supported:
		if the location is SobaA:
			execute JavaScript command "localStorage.setItem('currentRoom', 'SobaA')";
		otherwise if the location is SobaB:
			execute JavaScript command "localStorage.setItem('currentRoom', 'SobaB')";
		otherwise if the location is SobaC:
			execute JavaScript command "localStorage.setItem('currentRoom', 'SobaC')";
		otherwise:
			execute JavaScript command "localStorage.setItem('currentRoom', '[printed name of location]')";
	continue the action.

The player has a number called weight. The weight of the player is 0.

After taking something:
	increase the weight of the player by 1;
	continue the action.
	
After dropping something:
	decrease the weight of the player by 1;
	continue the action.
	
Part - Closet

The third coin is a coin in the Closet.
After taking the third coin:
	say "Taken.";
	increase the weight of the player by 1;
	update capacity counter.

Part - Kitchen

The perfume is a thing.
The description is "Ne smijes ovo izgubiti inace ne mozes u gallery ili napisi kod Irene koliko je kost bitna i napisi da ti prikriva miris."

Irena is a woman in the Kitchen. The description of Irena is "Ovo je opis Irene". Irena carries the perfume.

Table of Irena Responses
Topic	Response			Index	Asked
"Topic1"	"Bas mi treba nekaj da se pocesljam"			"Topic1"	false
	
After asking Irena about something:
	if the topic understood is a topic listed in the Table of Irena Responses:
		say "[Response entry][line break]";
		
After examining Irena: [mozes staviti insted pa nema You see nothing special about Irena.]
	say "Topics you can talk about with Irena:[line break]";
	repeat with N running from 1 to the number of rows in the Table of Irena Responses:
		choose row N in the Table of Irena Responses;
		say " - ";
		place a link to command "ask Irena about [Index entry]" reading "[Index entry]";
		if the Asked entry is true:
			say " (discussed)";
		say "[line break]".
		
Rule for printing the name of Irena:
	place a link to command "examine [the printed name of the item described]" reading "[the printed name of the item described]";
	
Instead of giving the brush to Irena:
	remove the brush from play;
	decrease the weight of the player by 1;
	update capacity counter;
	try Irena dropping the perfume.
	
The fridge is a container.
The description is "A large refrigerator with a four-digit lock.".
It is in the Kitchen.
It is closed, openable, locked and fixed in place.
The fridge has a truth state called code. Code of the fridge is false.

Instead of opening the fridge when the fridge is locked:
	say "It's locked. There seems to be a keypad lock on this.";
	now the command prompt is "Please enter the code > ".
	
After reading a command when the command prompt is "Please enter the code > ":
	if the player's command matches "5873":
		say "The lock beeps and clicks open!";
		play music file "achievement.mp3";
		award the "Fable enjoyer" achievement;
		now the fridge is unlocked;
		now the code of the fridge is true;
		now the command prompt is ">";
	otherwise:
		say "The lock beeps angrily. Wrong code!";
		now the command prompt is ">";
		play music file "wilhelm.mp3";
		kill the player in the Kitchen;
	reject the player's command.

The bone is a thing.
The description is "Ne smijes ovo izgubiti inace ne mozes u gallery ili napisi kod Irene koliko je kost bitna i napisi da ti prikriva miris."
It is in the fridge.

Part - Dining room

The rope is in the Dining room.
It is wearable.
The description is "This will come in handy. Napisi kak je bitno."

Instead of taking the rope:
	if the step stool is not in the Dining room:
		say "The rope is hanging too high up. You can't reach it from here. Maybe if you had something to stand on...";
	[otherwise if we have not taken the rope:
		say "Taken.";
		now the player carries the rope;
		increase the weight of the player by 1;
		update capacity counter;]
	otherwise:
		continue the action.
		
The mysterious note is in the Dining room.
The description is "Kao da se prepozanjes u ovome.".

After examining the mysterious note:
	if Vorple is supported:
		place an image "note6.png" with the description "Note6", centered.

Part - Pantry

The step stool is in the Pantry.
It is pushable between rooms and fixed in place.
The description is "Napisi kak je bitno da se dode do uzeta."

The kitchen note is in the Pantry.
The description is "A polybius square used to construct and decode secret messages."
After examining the kitchen note:
	if Vorple is supported:
		place an image "note4.png" with the description "Note4", centered.

Part - Attic

[The Attic has a time called the opening hour.
The opening hour of the Attic is 10:30 AM.
The Attic has a time called the closing hour.
The closing hour of the Attic is 10:45 AM.

Check going to the Attic:
	if the time of day is before the opening hour of the Attic,
	say "The attic is locked until [the opening hour of the Attic] because of the heat." instead.
	
Check going to the Attic:
	if the time of day is after the closing hour of the Attic:
		say "The attic is locked until [the closing hour of the Attic] because of the heat." instead.
	

Every turn when the location is the Attic:
	let deadline be the closing hour of the location;
	if the deadline is before the time of day:
		say "Because of the intense heat, you had to leave the attic.";
		move the player to Dining room.]

Chapter - Attic items

The black chest is a container.
It is in the Attic.
It is closed, openable and locked.

The Shop is a thing in the Attic.

The black key is in the Shop.
The black key unlocks the black chest.

The armor is in the Shop.
It is wearable.

Rule for printing the name of Shop while listing contents of a room:
	say "Shop";
	omit contents in listing.

Table of Shop Items
Item	Cost	Purchased
Black key	$80	false
Armor	$510	false

Instead of examining the Shop:
	if Vorple is supported:
		say "[We] [can] see the following items for sale:[line break]";
		open HTML tag "table" called "table1";
		open HTML tag "tr";
		place "th" element reading "Item";
		place "th" element reading "Price";
		close HTML tag;
		repeat through the Table of Shop Items:
			if Purchased entry is false:
				open HTML tag "tr";
				place "td" element called "td1" reading "[Item entry]";
				place "td" element called "td1" reading "[Cost entry]";
				close HTML tag;
		close HTML tag;
	otherwise:
		say "You see various items for sale."
		
Price is a kind of value. $1000 specifies a price.

The player has a price. The price of the player is $0.
The money has a price. The price of the money is $1500.

After taking the money:
	say "Taken.[line break]";
	increase the price of the player by the price of the money;
	update capacity counter;
	increase the weight of the player by 1.

After dropping the money:
	say "Dropped.[line break]";
	decrease the price of the player by the price of the money;
	update capacity counter;
	decrease the weight of the player by 1.
	
Instead of taking something which is in the Shop:
	say "You'll need to buy that first."

Purchase is an action applying to one thing.
Understand "buy [thing]" as purchase.

[Check purchase:
	let price wanted be $0;
	repeat through Table of Shop Items:
		if Item entry is the noun:
			now price wanted is Cost entry;
	if the price of the player is less than price wanted:
		say "You don't have enough money. [The noun] costs [price wanted], but you only have [price of player]." instead.]
		
Check purchase:
	if the noun is not in the Shop:
		say "That's not for sale." instead;
	let price wanted be $0;
	let found be false;
	repeat through Table of Shop Items:
		if Item entry is the noun:
			if Purchased entry is true:
				say "You already bought [the noun]." instead;
			now found is true;
			now price wanted is Cost entry;
	[if found is false:
		say "That's not for sale." instead;]
	if the price of the player is less than price wanted:
		say "You don't have enough money. [The noun] costs [price wanted], but you only have [price of player]." instead.

[Carry out purchase:
	let price wanted be $0;
	repeat through Table of Shop Items:
		if Item entry is the noun:
			now price wanted is Cost entry;
	decrease the price of the player by price wanted;
	if the player carries the money:
		decrease the price of the money by price wanted;
	now the noun is in the location; [now the player carries the noun]
	say "You pay [price wanted] and acquire [the noun].".]
	
Carry out purchase:
	let price wanted be $0;
	repeat through Table of Shop Items:
		if Item entry is the noun:
			now price wanted is Cost entry;
			now Purchased entry is true;
	decrease the price of the player by price wanted;
	if the player carries the money:
		decrease the price of the money by price wanted;
	move the noun to the Attic;
	say "You pay [price wanted] and acquire [the noun].".
	
Report purchase:
	say "You now have [price of player] remaining in your wallet.".

Part - Study

After reading a command:
	if the player's command matches the text "[player's full name]" or the player's command matches the text "say [player's full name]":
		replace the matched text with "cast name";
		continue the action.

Understand "cast name" as casting player name.
Casting player name is an action applying to nothing.

Check casting player name:
	if the player is not in the Study:
		say "Yes, that’s your name, but that doesn’t help you right now." instead.

Carry out casting player name:
	say "You disappear into a cloud of fog.";
	now the player is in the Bedroom.

Part - Bedroom (Ending)

Understand "meow" as casting meow.

Casting meow is an action applying to nothing.

Check casting meow:
	if the player is in the Bedroom:
		play music file "achievement.mp3";
		award the "Neno is waiting" achievement;
		end the story finally saying "Ah, there you are. Come on, we need to get ready - our vet appointment’s at 5.";
	otherwise:
		say "No one hears you."

Part - Achievements

Table of Achievements
Achievement	Description	Validation	Awarded
"Getting started"	"Finish the tutorial."	a rule	false
"StationPlay"	"Solve the desk puzzle."	--	false
"Let there be light"	"Turn on the generator."	--	false
"Mate&Teo"	"Solve the bookshelf puzzle."	--	false
"Best candidate for the job"	"Win a prize on the game machine."	--	false
"Neno is waiting"	"Finish the game."	--	false
"Enjoying the pain"	"Finish the game on unforgiving difficulty."	--	false
"8 more left"	"Die for the first time."	--	false
"Fable enjoyer"	"Unlock the fridge."	--	false
"Curiosity killed ..."	"Open the blackchest."	--	false

To award the (A - text) achievement:
	repeat through the Table of Achievements:
		if achievement entry is A:
			now awarded entry is true;
			display a success notification reading "You've earned an achievement: [A]";
			stop.
			
Understand "all achievements" as listing all achievements.
Listing all achievements is an action out of world.

Carry out listing all achievements:
	let unlocked be 0;
	let locked be 0;
	repeat through Table of Achievements:
		if awarded entry is true:
			increase unlocked by 1;
		otherwise:
			increase locked by 1;
	if unlocked > 0:
		say "You have unlocked the following achievements:[paragraph break]";
		repeat through Table of Achievements:
			if awarded entry is true:
				say "[bold type][achievement entry]:[roman type] [description entry][line break]";
	otherwise:
		say "You haven't unlocked any achievements yet.[paragraph break]";
	if locked > 0:
		say "[line break]Locked achievements:[paragraph break]";
		repeat through Table of Achievements:
			if awarded entry is false:
				say "[italic type][achievement entry][roman type][line break]";
				
To check achievements:
	execute JavaScript command "
		let ach = document.createElement('div');
		ach.id = 'achievements';
		ach.onclick = () => vorple.prompt.submit('all achievements');
		document.body.appendChild(ach);
	".